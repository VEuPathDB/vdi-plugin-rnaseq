#!/usr/bin/perl

use strict;
use DBI;
use FindBin;
use lib "$FindBin::Bin/../lib/perl";
use BigWigUtils;


my @envVars = ('DB_HOST', 'DB_PORT', 'DB_NAME', 'DB_PLATFORM', 'DB_USER', 'DB_PASS', 'DB_SCHEMA');

my ($userDatasetId) = @ARGV;

usage() unless scalar(@ARGV) == 1;

for my $envVar (@envVars) { die "Missing env variable '$envVar'\n" unless $ENV{$envVar}; }

my $dbSchema = $ENV{DB_SCHEMA};

##################################################
# Plugin handler service will remove the DATA_FILES dir for this UD
##################################################

##################################################
# remove from appdb
##################################################
my $connectString = $ENV{DB_PLATFORM} eq 'Oracle'?
    "dbi:Oracle://$ENV{DB_HOST}:$ENV{DB_PORT}/$ENV{DB_NAME}" :
    "DBI:Pg:dbname=$ENV{DB_NAME};host=$ENV{DB_HOST};port=$ENV{DB_PORT}";

my $dbh = DBI->connect($connectString, $ENV{DB_USER}, $ENV{DB_PASS}) || die "Couldn't connect to database: " . DBI->errstr;

$dbh->{RaiseError} = 1;
$dbh->{AutoCommit} = 0;

my $deleteProtocolAppNodes = $dbh->prepare(<<EOF);
    delete from $dbSchema.UD_ProtocolAppNode
    where profile_set_id
          in (select profile_set_id
              from $dbSchema.ud_ProfileSet
              where user_dataset_id = ?)
EOF


my $deleteNaFeatureExpression = $dbh->prepare(<<EOF);
    DELETE from $dbSchema.UD_NAFeatureExpression
    WHERE protocol_app_node_id in (select protocol_app_node_id 
                                   from $dbSchema.UD_ProtocolAppNode
                                   where profile_set_id
                                         in (select profile_set_id
                                             from $dbSchema.ud_ProfileSet
                                             where user_dataset_id = ?))
EOF

my $deleteProfileSet = $dbh->prepare(<<EOF);
    DELETE from $dbSchema.UD_ProfileSet
    WHERE user_dataset_id = ?
EOF

print STDERR "deleting from table $dbSchema.UD_NAFeatureExpression\n";
$deleteNaFeatureExpression->execute($userDatasetId);

print STDERR "deleting from table $dbSchema.UD_ProtocolAppNode\n";
$deleteProtocolAppNodes->execute($userDatasetId);

print STDERR "deleting from table $dbSchema.UD_ProfileSet\n";
$deleteProfileSet->execute($userDatasetId);

$dbh->commit;

sub usage {

  my $envStr = '$' . join(", \$", @envVars);

  die "
Uninstall an RNA Seq user dataset from the AppDB user dataset schema, and from jbrowse track files.

Usage: uninstall user_dataset_id 

Where:
  user_dataset_id:  a user dataset id

Env: $envStr

";
}

