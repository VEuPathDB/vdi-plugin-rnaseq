#!/usr/bin/perl

use strict;
use DBI;
use DBI qw(:sql_types);
use FindBin;
use lib "$FindBin::Bin/../lib/perl";
use RnaSeqHandlerCommon;
use DbiBatchExecutor;
use BigWigUtils;
use File::Copy;

my $DEBUG = 0;

my @envVars = ('DB_HOST', 'DB_PORT', 'DB_NAME', 'DB_PLATFORM', 'DB_USER', 'DB_PASS', 'DB_SCHEMA', 'DATA_FILES');

sub usage {
  my $envStr = '$' . join(", \$", @envVars);

  die "
Install an RNASeq user dataset into a website.

The dataset will include one or more .txt files (FPKM), and, optionally, parallel .bw files.  The
former are installed into the AppDB;  the latter into web service files.

Usage: install-data user_dataset_id filesDir
Where:
  user_dataset_id:  a user dataset id
  filesDir:         directory containing data files.  See import script for details.

ASSUMES THE DATASET IS PRE-VALIDATED DURING IMPORT.  See the import script.

Env: $envStr
";
}

my ($userDatasetId, $filesDir) = @ARGV;

usage() unless scalar(@ARGV) == 2;

for my $envVar (@envVars) { die "Missing env variable '$envVar'\n" unless $ENV{$envVar}; }

my $dbSchema = $ENV{DB_SCHEMA};

####################################################
# initialization for installing bw files to file sys
####################################################
my $datasetDir = $ENV{DATA_FILES};
die "Dataset target dir $datasetDir already exists\n" if -e $datasetDir;
mkdir($datasetDir) || die "Failed creating dataset target dir $datasetDir\n";
chmod(0775, $datasetDir) || die "Failed chmod of $datasetDir\n"; # rwxrwxr-x

##################################################
# initialization for installing txt files to appdb
##################################################
my $connectString = $ENV{DB_PLATFORM} eq 'Oracle'?
    "dbi:Oracle://$ENV{DB_HOST}:$ENV{DB_PORT}/$ENV{DB_NAME}" :
    "DBI:Pg:dbname=$ENV{DB_NAME};host=$ENV{DB_HOST};port=$ENV{DB_PORT}";

my $dbh = DBI->connect($connectString, $ENV{DB_USER}, $ENV{DB_PASS}) || die "Couldn't connect to database: " . DBI->errstr;

$dbh->{RaiseError} = 1;
$dbh->{AutoCommit} = 0;
my %profileSetIds;
my $orderNum = 1;
my $count = 0;
my %geneIds;
my ($panPkSh, $psPkSh, $insertProfileSet, $updateProfileSetUnit, $insertProtocolAppNode, $insertNaFeatureExpression, $unswitchProfiles)  = initializePreparedStmts();

#################################################################################
# iterate through lines of manifest file, processing each file (either txt or bw)
#################################################################################

open my $fh, "$filesDir/$MANIFEST_FILE" or die "Can't open manifest file '$filesDir/$MANIFEST_FILE' for reading:  $!";

# manifest file columns:
#    sampleName fileName strandInfo ('unstranded', 'sense' or 'antisense')
while(my ($panName, $filename, $strand) = readAndValidateManifestLine($fh, $filesDir)) {
  if ($filename =~ /(.+)\.txt$/) {
    installTxtFile($userDatasetId, $panName, "$filesDir/$filename", $strand);
  } else {
    installBwFile("$filesDir/$filename", $datasetDir);
  }
}

######################################
# do final processing for appdb
######################################
$unswitchProfiles->execute($userDatasetId);
$dbh->commit;



################################ subroutines   ##################################################################

sub initializePreparedStmts {

  my $panPkSql = $ENV{DB_PLATFORM} eq 'Oracle'?
      "select $dbSchema.UD_ProtocolAppNode_sq.nextval from dual":
      "select nextval('$dbSchema.UD_ProtocolAppNode_sq')";
  my $panPkSh = $dbh->prepare($panPkSql);

  my $psPkSql =  = $ENV{DB_PLATFORM} eq 'Oracle'?
      "select $dbSchema.UD_ProfileSet_sq.nextval from dual":
      "select nextval('$dbSchema.UD_ProfileSet_sq')";
  my $psPkSh = $dbh->prepare($psPkSql);

  my $insertProfileSet = $dbh->prepare(<<EOF);
    INSERT INTO $dbSchema.UD_ProfileSet
    (user_dataset_id, profile_set_id, name)
    VALUES (?,?,?)
EOF

  my $updateProfileSetUnit = $dbh->prepare(<<EOF);
    UPDATE $dbSchema.UD_ProfileSet
    SET unit = ?
    WHERE profile_set_id = ?
EOF

  my $insertProtocolAppNode = $dbh->prepare(<<EOF);
    INSERT INTO $dbSchema.UD_ProtocolAppNode
    (protocol_app_node_id, profile_set_id, name, node_order_num)
    VALUES (?,?,?,?)
EOF

  my $seqNxtVal = $ENV{DB_PLATFORM} eq 'Oracle'?
      "$dbSchema.UD_NaFeatureExpression_sq.nextval":
      "nextval('$dbSchema.UD_NaFeatureExpression_sq')";
      
  my $insertNaFeatureExpression = $dbh->prepare(<<EOF);
    INSERT INTO $dbSchema.UD_NaFeatureExpression
    (na_feat_expression_id, protocol_app_node_id, na_feature_id, value)
    VALUES ($seqNxtVal,?,?,?)
EOF

  # query to switch the sense of stranded data if sense.median < antisense.median
  my $unswitchProfiles = $dbh->prepare(<<EOF);
update $dbSchema.ud_ProfileSet
set name = case name
             when 'sense' then 'antisense'
             when 'antisense' then 'sense'
             else name
           end
where user_dataset_id = ?
  and user_dataset_id
      in (select sense.user_dataset_id
          from (select ps.user_dataset_id, median(nfe.value) as median
                from $dbSchema.ud_NAFeatureExpression nfe,
                     $dbSchema.ud_ProfileSet ps,
                     $dbSchema.ud_ProtocolAppNode pan
                where ps.name = 'sense'
                  and ps.profile_set_id = pan.profile_set_id
                  and pan.protocol_app_node_id = nfe.protocol_app_node_id
                group by ps.user_dataset_id, ps.name) sense,
               (select ps.user_dataset_id, median(nfe.value) as median
                from $dbSchema.ud_NAFeatureExpression nfe,
                     $dbSchema.ud_ProfileSet ps,
                     $dbSchema.ud_ProtocolAppNode pan
                where ps.name = 'antisense'
                  and ps.profile_set_id = pan.profile_set_id
                  and pan.protocol_app_node_id = nfe.protocol_app_node_id
                group by ps.user_dataset_id, ps.name) antisense
          where sense.user_dataset_id = antisense.user_dataset_id
            and sense.median < antisense.median)
EOF

    return ($panPkSh, $psPkSh, $insertProfileSet, $updateProfileSetUnit, $insertProtocolAppNode, $insertNaFeatureExpression, $unswitchProfiles);

}

sub installTxtFile {
  my ($userDatasetId, $panName, $filename, $strand) = @_;

  print STDERR "installing txt file $filename\n" if $DEBUG;
  my $psId;
  if ($profileSetIds{$strand}) {
    $psId = $profileSetIds{$strand};
  }
  else {
    print STDERR "executing query psPkSh\n" if $DEBUG;
    $psPkSh->execute();
    ($psId) = $psPkSh->fetchrow_array();

    print STDERR "executing insert profile set $psId $strand\n" if $DEBUG;
    $insertProfileSet->execute($userDatasetId, $psId, $strand);

    $profileSetIds{$strand} = $psId;
  }

  print STDERR "executing query panPkSh\n if $DEBUG";
  $panPkSh->execute();
  my ($panId) = $panPkSh->fetchrow_array();

  print STDERR "executing insert protocol app node $psId $panId $panName $orderNum\n if $DEBUG";
  $insertProtocolAppNode->execute($panId, $psId, $panName, $orderNum);

  my ($idIndex, $fpkmIndex);
  print STDERR "opening fpkm file $filename\n if $DEBUG";
  open(DAT, $filename) or die "Cannot open file $filename for reading: $!";

  # check header to distinguish file type
  my $unit;
  my $header = <DAT>;
  chomp($header);

  if ($header eq "tracking_id\tclass_code\tnearest_ref_id\tgene_id\tgene_short_name\ttss_id\tlocus\tlength\tcoverage\tFPKM\tFPKM_conf_lo\tFPKM_conf_hi\tFPKM_status") {
    # Cufflinks FPKM file
    $idIndex = 0;
    $fpkmIndex = 9;
    $unit = "FPKM";
  } elsif ($header eq "gene_id\tFPKM") {
    # FPKMtool file
    $idIndex = 0;
    $fpkmIndex = 1;
    $unit = "FPKM";
  } elsif ($header eq "gene_id\tTPM") {
    # TPMtool file
    $idIndex = 0;
    $fpkmIndex = 1;
    $unit = "TPM";
  } else {
    validationError("Unrecognized header record. Not a valid transcript abundance file. File: $filename");
  }

  print STDERR "execute update profile set unit $psId $unit\n if $DEBUG";
  $updateProfileSetUnit->execute($unit, $psId);

  print STDERR "iterate through fpkm file, inserting to na feature expression for each source id.  commit every 1000 rows\n if $DEBUG";

  my @panIdArray;
  my @naFeatureIdArray;
  my @fpkmArray;
  my $batchExecutor = new DbiBatchExecutor(100, 1000);

#  $insertNaFeatureExpression->bind_param(1, 1.0, { TYPE => SQL_DOUBLE });
  while(<DAT>) {
    chomp;

    my @a = split(/\t/, $_);

    my $sourceId = $a[$idIndex];
    my $fpkm = $a[$fpkmIndex];

    next unless($sourceId);

    unless(%geneIds) {
      &makeGeneSourceIdMapping($sourceId, $dbh); # use this gene's source id to find the relevant organism and initialize mapping of all its source_ids.
    }

    my $naFeatureId = $geneIds{$sourceId};
    unless($naFeatureId) {
      print STDERR "WARN: Could not find an na_feature_id for source_id $sourceId" if $DEBUG;
      next;
    }
    push(@panIdArray, $panId);
    push(@naFeatureIdArray, $naFeatureId);
    push(@fpkmArray, $fpkm);
    $batchExecutor->periodicallyExecuteBatch($dbh, $insertNaFeatureExpression, 0, [SQL_INTEGER, SQL_INTEGER, SQL_DOUBLE], \@panIdArray, \@naFeatureIdArray, \@fpkmArray);

  }
  $batchExecutor->periodicallyExecuteBatch($dbh, $insertNaFeatureExpression, 1, [SQL_INTEGER, SQL_INTEGER, SQL_DOUBLE], \@panIdArray, \@naFeatureIdArray, \@fpkmArray);

  $orderNum++;
}

sub makeGeneSourceIdMapping {
  my ($sourceId, $dbh) = @_;

  my $sql = "select source_id, na_feature_id from apidbtuning.geneattributes where organism in (select organism from apidbtuning.geneattributes where source_id = '$sourceId')";

  print STDERR "execute query to map source ids to feature ids\n"; # too verbose in the logs
  my $sh = $dbh->prepare($sql);
  $sh->execute();

  while(my ($sourceId, $naFeatureId) = $sh->fetchrow_array()) {
    $geneIds{$sourceId} = $naFeatureId;
  }
  $sh->finish();
}

sub validationError {
  my ($msg) = @_;

  print STDOUT "$msg\n";
  exit($VALIDATION_ERROR_CODE);
}


